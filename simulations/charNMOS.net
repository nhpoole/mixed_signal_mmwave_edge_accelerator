Characterize N Channel MOSFET

.lib /home/users/nhpoole/skywater-pdk/libraries/sky130_fd_pr_ngspice/latest/models/sky130.lib.spice tt

vds  nDrain 0 dc 0
vgs  nGate  0 dc 0
vbs  nBulk  0 dc 0

xn1 nDrain nGate 0 nBulk sky130_fd_pr__nfet_01v8_lvt L=0.15 W=1
xn2 nDrain nGate 0 nBulk sky130_fd_pr__nfet_01v8_lvt L=0.15 W=1 nf=1 ad='int((nf+1)/2) * W/nf * 0.29' as='int((nf+2)/2) * W/nf * 0.29'
+ pd='2*int((nf+1)/2) * (W/nf + 0.29)' ps='2*int((nf+2)/2) * (W/nf + 0.29)' nrd='0.29 / W' nrs='0.29 / W'
+ sa=0 sb=0 sd=0 mult=1 m=1

.options dccap post brief accurate
.control
save all @m.xn.msky130_fd_pr__nfet_01v8_lvt[id] 
+ @m.xn1.msky130_fd_pr__nfet_01v8_lvt[vth]
+ @m.xn1.msky130_fd_pr__nfet_01v8_lvt[gm]
+ @m.xn1.msky130_fd_pr__nfet_01v8_lvt[gmbs] 
+ @m.xn1.msky130_fd_pr__nfet_01v8_lvt[gds] 
+ @m.xn1.msky130_fd_pr__nfet_01v8_lvt[cgg] 
+ @m.xn1.msky130_fd_pr__nfet_01v8_lvt[cgs] 
+ @m.xn1.msky130_fd_pr__nfet_01v8_lvt[cgd] 
+ @m.xn1.msky130_fd_pr__nfet_01v8_lvt[cdd] 
+ @m.xn1.msky130_fd_pr__nfet_01v8_lvt[cbs] 
+ @m.xn2.msky130_fd_pr__nfet_01v8_lvt[id] 
+ @m.xn2.msky130_fd_pr__nfet_01v8_lvt[vth]
+ @m.xn2.msky130_fd_pr__nfet_01v8_lvt[gm]
+ @m.xn2.msky130_fd_pr__nfet_01v8_lvt[gmbs] 
+ @m.xn2.msky130_fd_pr__nfet_01v8_lvt[gds] 
+ @m.xn2.msky130_fd_pr__nfet_01v8_lvt[cgg] 
+ @m.xn2.msky130_fd_pr__nfet_01v8_lvt[cgs] 
+ @m.xn2.msky130_fd_pr__nfet_01v8_lvt[cgd] 
+ @m.xn2.msky130_fd_pr__nfet_01v8_lvt[cdd] 
+ @m.xn2.msky130_fd_pr__nfet_01v8_lvt[cbs] 

dc vgs 0 1.9 0.02 vds 0 1.9 0.02

let id1   = @m.xn1.msky130_fd_pr__nfet_01v8_lvt[id]
let vt1   = @m.xn1.msky130_fd_pr__nfet_01v8_lvt[vth]
let gm1   = @m.xn1.msky130_fd_pr__nfet_01v8_lvt[gm]
let gmb1  = @m.xn1.msky130_fd_pr__nfet_01v8_lvt[gmbs]
let gds1  = @m.xn1.msky130_fd_pr__nfet_01v8_lvt[gds]
let cgg1  = @m.xn1.msky130_fd_pr__nfet_01v8_lvt[cgg]
let cgs1  = -@m.xn1.msky130_fd_pr__nfet_01v8_lvt[cgs]
let cgd1  = -@m.xn1.msky130_fd_pr__nfet_01v8_lvt[cgd]
let cgb1  = @m.xn1.msky130_fd_pr__nfet_01v8_lvt[cgg] - (-@m.xn1.msky130_fd_pr__nfet_01v8_lvt[cgs])-(-@m.xn1.msky130_fd_pr__nfet_01v8_lvt[cgd])
let cdd1  = @m.xn1.msky130_fd_pr__nfet_01v8_lvt[cdd]
let css1  = -@m.xn1.msky130_fd_pr__nfet_01v8_lvt[cgs]-@m.xn1.msky130_fd_pr__nfet_01v8_lvt[cbs]
let id2   = @m.xn2.msky130_fd_pr__nfet_01v8_lvt[id]
let vt2   = @m.xn2.msky130_fd_pr__nfet_01v8_lvt[vth]
let gm2   = @m.xn2.msky130_fd_pr__nfet_01v8_lvt[gm]
let gmb2  = @m.xn2.msky130_fd_pr__nfet_01v8_lvt[gmbs]
let gds2  = @m.xn2.msky130_fd_pr__nfet_01v8_lvt[gds]
let cgg2  = @m.xn2.msky130_fd_pr__nfet_01v8_lvt[cgg]
let cgs2  = -@m.xn2.msky130_fd_pr__nfet_01v8_lvt[cgs]
let cgd2  = -@m.xn2.msky130_fd_pr__nfet_01v8_lvt[cgd]
let cgb2  = @m.xn2.msky130_fd_pr__nfet_01v8_lvt[cgg] - (-@m.xn2.msky130_fd_pr__nfet_01v8_lvt[cgs])-(-@m.xn2.msky130_fd_pr__nfet_01v8_lvt[cgd])
let cdd2  = @m.xn2.msky130_fd_pr__nfet_01v8_lvt[cdd]
let css2  = -@m.xn2.msky130_fd_pr__nfet_01v8_lvt[cgs]-@m.xn2.msky130_fd_pr__nfet_01v8_lvt[cbs]

write outN.raw id1 vt1 gm1 gmb1 gds1 cgg1 cgs1 cgd1 cgb1 cdd1 css1 id2 vt2 gm2 gmb2 gds2 cgg2 cgs2 cgd2 cgb2 cdd2 css2
exit
.endc
.end
